#!/usr/bin/expect -f
###########################################################
# This automates the process of generating the setup
# tarball, transferring it to the BeagleBone Black, and
# running the setup script, where the user will start
# providing inputs.
###########################################################

set scriptDir [file dirname [file normalize [info script]]]

set beagleBoneIPAddress       192.168.6.2
set beagleBoneUserName        debian
set beagleBoneDefaultPasscode temppwd

set setupTarball neighvarroSetup.tar
set setupDir     neighvarroSetup
set setupScript  NeighvarroSetup.bash

###########################################################
#  Primary Functions  #
#######################

proc exitIfTheBeagleBoneCannotBePinged {} {
	
	global beagleBoneIPAddress
	
	if [pingHost $beagleBoneIPAddress] {
		return
	} else {
		coloredPuts "Beaglebone could not be pinged.  Retry once the tether is established."
		exit
	}
}

proc makeAppSetupTarball {} {
	
	global scriptDir
	
	set timeout 120
	spawn $scriptDir/makeAppSetupTarball.bash
	expect eof
}

proc deleteAppSetupTarball {} {
	
	global scriptDir setupTarball
	
	spawn rm $scriptDir/$setupTarball
	expect eof
}

proc scpTarballToBeagleBone {} {
	
	global beagleBoneIPAddress beagleBoneUserName \
			beagleBoneDefaultPasscode setupTarball \
			scriptDir
	
	# Usually takes about 28 seconds on my desktop.
	# Setting to 4 minutes for margin.
	set timeout 240
	spawn scp -o UserKnownHostsFile=/dev/null \
			-o StrictHostKeyChecking=no \
			$scriptDir/$setupTarball \
			$beagleBoneUserName@$beagleBoneIPAddress:
	expect "password: "
	send "$beagleBoneDefaultPasscode\r"
	expect eof
}

proc untarAndRunSetupOnBeagleBone {} {
	
	global beagleBoneIPAddress beagleBoneUserName \
			beagleBoneDefaultPasscode setupTarball \
			setupDir setupScript
	
	set prompt "$ "
	
	set timeout 10
	spawn ssh -o UserKnownHostsFile=/dev/null \
			-o StrictHostKeyChecking=no \
			$beagleBoneUserName@$beagleBoneIPAddress
	expect "password: "
	send "$beagleBoneDefaultPasscode\r"
	
	expect "$prompt"
	set timeout 240
	send "tar xmf $setupTarball\r"
	
	expect "$prompt"
	coloredPuts "\n************************\n*  Begin Setup Script  *\n************************"
	send "./$setupDir/$setupScript\r"
	expect "password for $beagleBoneUserName: "
	send "$beagleBoneDefaultPasscode\r"
	
	interact
}

proc remindToRunTests {} {
	
	coloredPuts "Remember to run the platform tests script after the board reboots."
}

###########################################################
#  Utility Functions  #
#######################

proc commandBreak {} {
	puts "\n----------------------------------------------------\n"
}

proc pingHost {hostname} {
	
	spawn ping -c 2 -W 1 "$hostname"
	
	expect {
		timeout {
			puts stderr "Unexpected ping timeout: $hostname"
			return false
		}
		"100% packet loss" {
			set returnFlag false
		}
		"time" {
			set returnFlag true
		}
	}
	expect eof
	
	return $returnFlag
}

proc coloredPuts {text} {
	global colorLightCyan
	puts "[colorText $colorLightCyan $text]"
}

set colorLightCyan 12

proc colorText {color text} {
	return [exec tput setaf $color]$text[exec tput sgr0]
}

###########################################################
#  Main  #
##########

exitIfTheBeagleBoneCannotBePinged
commandBreak
makeAppSetupTarball
commandBreak
scpTarballToBeagleBone
commandBreak
untarAndRunSetupOnBeagleBone
deleteAppSetupTarball
remindToRunTests
